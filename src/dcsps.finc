!*==dcsps.f90 processed by SPAG 8.01RF 00:35  2 Mar 2025

!
!     ..................................................................
!
!        SUBROUTINE DCSPS
!
!        PURPOSE
!           COMPUTES THE VALUE OF AN N-TERM EXPANSION IN SHIFTED
!           CHEBYSHEV POLYNOMIALS WITH COEFFICIENT VECTOR C
!           FOR ARGUMENT VALUE X.
!
!        USAGE
!           CALL DCSPS(Y,X,C,N)
!
!        DESCRIPTION OF PARAMETERS
!           Y     - RESULT VALUE
!                   DOUBLE PRECISION VARIABLE
!           X     - ARGUMENT VALUE
!                   DOUBLE PRECISION VARIABLE
!           C     - COEFFICIENT VECTOR OF GIVEN EXPANSION
!                   COEFFICIENTS ARE ORDERED FROM LOW TO HIGH
!                   DOUBLE PRECISION VECTOR
!           N     - DIMENSION OF COEFFICIENT VECTOR C
!
!        REMARKS
!           OPERATION IS BYPASSED IN CASE N LESS THAN 1
!
!        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
!           NONE
!
!        METHOD
!           DEFINITION
!           Y=SUM(C(I)*TS(I-1,X), SUMMED OVER I FROM 1 TO N).
!           EVALUATION IS DONE BY MEANS OF BACKWARD RECURSION
!           USING THE RECURRENCE EQUATION FOR SHIFTED
!           CHEBYSHEV POLYNOMIALS
!           TS(N+1,X)=(4*X-2)*TS(N,X)-TS(N-1,X).
!
!     ..................................................................
!
SUBROUTINE dcsps(Y,X,C,N)
   IMPLICIT NONE

   INTEGER i,k,N

!
   DIMENSION C(*)
   DOUBLE PRECISION C,Y,X,h0,h1,h2,arg
!
!        TEST OF DIMENSION
   IF ( N<=0 ) THEN
      RETURN
!
   ELSEIF ( N<2 ) THEN
      Y = C(1)
      RETURN
   ELSE
!
!        INITIALIZATION
      arg = X + X - 1.D0
      arg = arg + arg
      h1 = 0.D0
      h0 = 0.D0
      DO i = 1,N
         k = N - i
         h2 = h1
         h1 = h0
         h0 = arg*h1 - h2 + C(k+1)
      ENDDO
      Y = 0.5D0*(C(1)-h2+h0)
   ENDIF
END SUBROUTINE dcsps
