!*==det5.f90 processed by SPAG 8.01RF 00:35  2 Mar 2025

!
!     ..................................................................
!
!        SUBROUTINE DET5
!
!        PURPOSE
!           TO COMPUTE A VECTOR OF DERIVATIVE VALUES GIVEN A VECTOR OF
!           FUNCTION VALUES WHOSE ENTRIES CORRESPOND TO EQUIDISTANTLY
!           SPACED ARGUMENT VALUES.
!
!        USAGE
!           CALL DET5(H,Y,Z,NDIM,IER)
!
!        DESCRIPTION OF PARAMETERS
!           H     -  CONSTANT DIFFERENCE BETWEEN SUCCESSIVE ARGUMENT
!                    VALUES (H IS POSITIVE IF THE ARGUMENT VALUES
!                    INCREASE AND NEGATIVE OTHERWISE)
!           Y     -  GIVEN VECTOR OF FUNCTION VALUES (DIMENSION NDIM)
!           Z     -  RESULTING VECTOR OF DERIVATIVE VALUES (DIMENSION
!                    NDIM)
!           NDIM  -  DIMENSION OF VECTORS Y AND Z
!           IER   -  RESULTING ERROR PARAMETER
!                    IER = -1  - NDIM IS LESS THAN 5
!                    IER =  0  - NO ERROR
!                    IER =  1  - H = 0
!
!        REMARKS
!           (1)   IF IER = -1,1, THEN THERE IS NO COMPUTATION.
!           (2)   Z CAN HAVE THE SAME STORAGE ALLOCATION AS Y.  IF Y IS
!                 DISTINCT FROM Z, THEN IT IS NOT DESTROYED.
!
!        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
!           NONE
!
!        METHOD
!           IF X IS THE (SUPPRESSED) VECTOR OF ARGUMENT VALUES, THEN
!           EXCEPT AT THE POINTS X(1),X(2),X(NDIM-1) AND X(NDIM), Z(I)
!           IS THE DERIVATIVE AT X(I) OF THE LAGRANGIAN INTERPOLATION
!           POLYNOMIAL OF DEGREE 4 RELEVANT TO THE 5 SUCCESSIVE POINTS
!           (X(I+K),Y(I+K)) K = -2,-1,...,2.  (SEE HILDEBRAND, F.B.,
!           INTRODUCTION TO NUMERICAL ANALYSIS, MC GRAW-HILL, NEW YORK/
!           TORONTO/LONDON, 1956, PP. 82-84.)
!
!     ..................................................................
!
SUBROUTINE det5(H,Y,Z,Ndim,Ier)
   IMPLICIT NONE

   REAL a,b,c,H,hh,Y,yy,Z
   INTEGER i,Ier,Ndim

!
!
   DIMENSION Y(*),Z(*)
!
!        TEST OF DIMENSION
   IF ( Ndim<5 ) THEN
!
!        ERROR EXIT IN CASE NDIM IS LESS THAN 5
      Ier = -1
      RETURN
!
!        TEST OF STEPSIZE
   ELSEIF ( H/=0 ) THEN
!
!        PREPARE DIFFERENTIATION LOOP
      hh = .08333333/H
      yy = Y(Ndim-4)
      b = hh*(-25.*Y(1)+48.*Y(2)-36.*Y(3)+16.*Y(4)-3.*Y(5))
      c = hh*(-3.*Y(1)-10.*Y(2)+18.*Y(3)-6.*Y(4)+Y(5))
!
!        START DIFFERENTIATION LOOP
      DO i = 5,Ndim
         a = b
         b = c
         c = hh*(Y(i-4)-Y(i)+8.*(Y(i-1)-Y(i-3)))
         Z(i-4) = a
      ENDDO
!        END OF DIFFERENTIATION LOOP
!
!        NORMAL EXIT
      Ier = 0
      a = hh*(-yy+6.*Y(Ndim-3)-18.*Y(Ndim-2)+10.*Y(Ndim-1)+3.*Y(Ndim))
      Z(Ndim) = hh*(3.*yy-16.*Y(Ndim-3)+36.*Y(Ndim-2)-48.*Y(Ndim-1)+25.*Y(Ndim))
      Z(Ndim-1) = a
      Z(Ndim-2) = c
      Z(Ndim-3) = b
      RETURN
   ELSE
!
!        ERROR EXIT IN CASE OF ZERO STEPSIZE
      Ier = 1
   ENDIF
END SUBROUTINE det5
