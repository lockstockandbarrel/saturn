!*==dlaps.f90 processed by SPAG 8.01RF 00:35  2 Mar 2025
!..................................................................
!   SUBROUTINE DLAPS
!
!   PURPOSE
!      COMPUTES THE VALUE OF AN N-TERM EXPANSION IN LAGUERRE
!      POLYNOMIALS WITH COEFFICIENT VECTOR C FOR ARGUMENT VALUE X.
!
!   USAGE
!      CALL DLAPS(Y,X,C,N)
!
!   DESCRIPTION OF PARAMETERS
!      Y     - RESULT VALUE
!      X     - ARGUMENT VALUE
!      C     - COEFFICIENT VECTOR OF GIVEN EXPANSION
!              COEFFICIENTS ARE ORDERED FROM LOW TO HIGH
!      N     - DIMENSION OF COEFFICIENT VECTOR C
!   NOTE: C,Y,X,H0,H1,H2 AND T ARE DOUBLE PRECISION
!   REMARKS
!      OPERATION IS BYPASSED IN CASE N LESS THAN 1
!
!   SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
!      NONE
!
!   METHOD
!      DEFINITION
!      Y=SUM(C(I)*L(I-1,X), SUMMED OVER I FROM 1 TO N).
!      EVALUATION IS DONE BY MEANS OF UPWARD RECURSION
!      USING THE RECURRENCE EQUATION FOR LAGUERRE POLYNOMIALS
!      L(N+1,X)=2*L(N,X)-L(N-1,X)-((1+X)*L(N,X)-L(N-1,X))/(N+1).
!..................................................................
SUBROUTINE dlaps(Y,X,C,N)
IMPLICIT NONE
INTEGER i, N
DIMENSION C(*)
DOUBLE PRECISION C, Y, X, h0, h1, h2, t
!  TEST OF DIMENSION
   IF ( N>0 ) THEN
!
      Y = C(1)
      IF ( N>=2 ) THEN
!
!        INITIALIZATION
         h0 = 1.
         h1 = 1. - X
         t = 1. + X
!
         DO i = 2, N
            h2 = h1 - h0 + h1 - (t*h1-h0)/dfloat(i)
            h0 = h1
            h1 = h2
            Y = Y + C(i)*h0
         ENDDO
         RETURN
      ENDIF
   ENDIF
END SUBROUTINE dlaps
