!*==smirn.f90 processed by SPAG 8.01RF 00:35  2 Mar 2025

!
!     ..................................................................
!
!        SUBROUTINE SMIRN
!
!        PURPOSE
!           COMPUTES VALUES OF THE LIMITING DISTRIBUTION FUNCTION FOR
!           THE KOLMOGOROV-SMIRNOV STATISTIC.
!
!        USAGE
!           CALL SMIRN(X,Y)
!
!        DESCRIPTION OF PARAMETERS
!           X    - THE ARGUMENT OF THE SMIRN FUNCTION
!           Y    - THE RESULTANT SMIRN FUNCTION VALUE
!
!        REMARKS
!           Y IS SET TO ZERO IF X IS NOT GREATER THAN 0.27, AND IS SET
!           TO ONE IF X IS NOT LESS THAN 3.1.  ACCURACY TESTS WERE MADE
!           REFERRING TO THE TABLE GIVEN IN THE REFERENCE BELOW.
!           TWO ARGUMENTS, X= 0.62, AND X = 1.87 GAVE RESULTS WHICH
!           DIFFER FROM THE SMIRNOV TABLES BY 2.9 AND 1.9 IN THE 5TH
!           DECIMAL PLACE.  ALL OTHER RESULTS SHOWED SMALLER ERRORS,
!           AND ERROR SPECIFICATIONS ARE GIVEN IN THE ACCURACY TABLES
!           IN THIS MANUAL.  IN DOUBLE PRECISION MODE, THESE SAME
!           ARGUMENTS RESULTED IN DIFFERENCES FROM TABLED VALUES BY 3
!           AND 2 IN THE 5TH  DECIMAL PLACE.  IT IS NOTED IN
!           LINDGREN (REFERENCE BELOW) THAT FOR HIGH SIGNIFICANCE LEVELS
!           (SAY, .01 AND .05) ASYMPTOTIC FORMULAS GIVE VALUES WHICH ARE
!           TOO HIGH ( BY 1.5 PER CENT WHEN N = 80).  THAT IS, AT HIGH
!           SIGNIFICANCE LEVELS, THE HYPOTHESIS OF NO DIFFERENCE WILL BE
!           REJECTED TOO SELDOM USING ASYMPTOTIC FORMULAS.
!
!        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
!           NONE
!
!        METHOD
!           THE METHOD IS DESCRIBED BY W. FELLER-ON THE KOLMOGOROV-
!           SMIRNOV LIMIT THEOREMS FOR EMPIRICAL DISTRIBUTIONS- ANNALS
!           OF MATH. STAT., 19, 1948, 177-189, BY N. SMIRNOV--TABLE
!           FOR ESTIMATING THE GOODNESS OF FIT OF EMPIRICAL
!           DISTRIBUTIONS- ANNALS OF MATH. STAT., 19, 1948, 279-281,
!           AND GIVEN IN LINDGREN, STATISTICAL THEORY, THE MACMILLAN
!           COMPANY, N. Y., 1962.
!
!     ..................................................................
!
SUBROUTINE smirn(X,Y)
   IMPLICIT NONE

   REAL q1,q2,q4,q8,X,Y

!     DOUBLE PRECISION X,Q1,Q2,Q4,Q8,Y
!
!        IF A DOUBLE PRECISION VERSION OF THIS ROUTINE IS DESIRED, THE C
!        IN COLUMN ONE OF THE DOUBLE PRECISION CARD ABOVE SHOULD BE
!        REMOVED, AND THE C IN COLUMN ONE OF THE STATEMENTS NUMBERED
!        C   3, C   5, AND C   8 SHOULD BE REMOVED, AND THESE CARDS
!        SHOULD REPLACE THE STATEMENTS NUMBERED 3, 5, AND 8,
!        RESPECTIVELY.  ALL ROUTINES CALLING THIS ROUTINE MUST ALSO
!        PROVIDE DOUBLE PRECISION ARGUMENTS TO THIS ROUTINE.
!
!     ..................................................................
!
   IF ( X<=.27 ) THEN
      Y = 0.0
   ELSEIF ( X<1.0 ) THEN
      q1 = exp(-1.233701/X**2)
!   3 Q1=DEXP(-1.233700550136170/X**2)
      q2 = q1*q1
      q4 = q2*q2
      q8 = q4*q4
      IF ( q8<1.0E-25 ) q8 = 0.0
!   5 Y=(2.506628274631001/X)*Q1*(1.0D0+Q8*(1.0D0+Q8*Q8))
      Y = (2.506628/X)*q1*(1.0+q8*(1.0+q8*q8))
   ELSEIF ( X<3.1 ) THEN
      q1 = exp(-2.0*X*X)
!   8 Q1=DEXP(-2.0D0*X*X)
      q2 = q1*q1
      q4 = q2*q2
      q8 = q4*q4
      Y = 1.0 - 2.0*(q1-q4+q8*(q1-q8))
   ELSE
      Y = 1.0
   ENDIF
END SUBROUTINE smirn
