!*==qhse.f90 processed by SPAG 8.01RF 00:35  2 Mar 2025

!
!     ..................................................................
!
!        SUBROUTINE QHSE
!
!        PURPOSE
!           TO COMPUTE THE VECTOR OF INTEGRAL VALUES FOR A GIVEN
!           EQUIDISTANT TABLE OF FUNCTION, FIRST DERIVATIVE,
!           AND SECOND DERIVATIVE VALUES.
!
!        USAGE
!           CALL QHSE (H,Y,FDY,SDY,Z,NDIM)
!
!        DESCRIPTION OF PARAMETERS
!           H      - THE INCREMENT OF ARGUMENT VALUES.
!           Y      - THE INPUT VECTOR OF FUNCTION VALUES.
!           FDY    - THE INPUT VECTOR OF FIRST DERIVATIVE.
!           SDY    - THE INPUT VECTOR OF SECOND DERIVATIVE.
!           Z      - THE RESULTING VECTOR OF INTEGRAL VALUES. Z MAY BE
!                    IDENTICAL WITH Y,FDY OR SDY.
!           NDIM   - THE DIMENSION OF VECTORS Y,FDY,SDY,Z.
!
!        REMARKS
!           NO ACTION IN CASE NDIM LESS THAN 1.
!
!        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
!           NONE
!
!        METHOD
!           BEGINNING WITH Z(1)=0, EVALUATION OF VECTOR Z IS DONE BY
!           MEANS OF HERMITEAN SIXTH ORDER INTEGRATION FORMULA.
!           FOR REFERENCE, SEE
!           R.ZURMUEHL, PRAKTISCHE MATHEMATIK FUER INGENIEURE UND
!           PHYSIKER, SPRINGER, BERLIN/GOETTINGEN/HEIDELBERG, 1963,
!           PP.227-230.
!
!     ..................................................................
!
SUBROUTINE qhse(H,Y,Fdy,Sdy,Z,Ndim)
   IMPLICIT NONE

   REAL Fdy,H,hf,hh,ht,Sdy,sum1,sum2,Y,Z
   INTEGER i,Ndim

!
!
   DIMENSION Y(*),Fdy(*),Sdy(*),Z(*)
!
   sum2 = 0.
   IF ( Ndim<1 ) RETURN
   IF ( Ndim/=1 ) THEN
      hh = .5*H
      hf = .2*H
      ht = .08333333*H
!
!     INTEGRATION LOOP
      DO i = 2,Ndim
         sum1 = sum2
         sum2 = sum2 + hh*((Y(i-1)+Y(i))+hf*((Fdy(i-1)-Fdy(i))+ht*(Sdy(i-1)+Sdy(i))))
         Z(i-1) = sum1
      ENDDO
   ENDIF
   Z(Ndim) = sum2
END SUBROUTINE qhse
