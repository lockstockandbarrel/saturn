!*==dqhse.f90 processed by SPAG 8.01RF 00:35  2 Mar 2025

!
!     ..................................................................
!
!        SUBROUTINE DQHSE
!
!        PURPOSE
!           TO COMPUTE THE VECTOR OF INTEGRAL VALUES FOR A GIVEN
!           EQUIDISTANT TABLE OF FUNCTION, FIRST DERIVATIVE,
!           AND SECOND DERIVATIVE VALUES.
!
!        USAGE
!           CALL DQHSE (H,Y,FDY,SDY,Z,NDIM)
!
!        DESCRIPTION OF PARAMETERS
!           H      - DOUBLE PRECISION INCREMENT OF ARGUMENT VALUES.
!           Y      - DOUBLE PRECISION INPUT VECTOR OF FUNCTION VALUES.
!           FDY    - DOUBLE PRECISION INPUT VECTOR OF FIRST DERIVATIVE.
!           SDY    - DOUBLE PRECISION INPUT VECTOR OF SECOND DERIVATIVE.
!           Z      - RESULTING DOUBLE PRECISION VECTOR OF INTEGRAL
!                    VALUES. Z MAY BE IDENTICAL WITH Y, FDY OR SDY.
!           NDIM   - THE DIMENSION OF VECTORS Y,FDY,SDY,Z.
!
!        REMARKS
!           NO ACTION IN CASE NDIM LESS THAN 1.
!
!        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
!           NONE
!
!        METHOD
!           BEGINNING WITH Z(1)=0, EVALUATION OF VECTOR Z IS DONE BY
!           MEANS OF HERMITEAN SIXTH ORDER INTEGRATION FORMULA.
!           FOR REFERENCE, SEE
!           R.ZURMUEHL, PRAKTISCHE MATHEMATIK FUER INGENIEURE UND
!           PHYSIKER, SPRINGER, BERLIN/GOETTINGEN/HEIDELBERG, 1963,
!           PP.227-230.
!
!     ..................................................................
!
SUBROUTINE dqhse(H,Y,Fdy,Sdy,Z,Ndim)
   IMPLICIT NONE

   INTEGER i,Ndim

!
!
   DIMENSION Y(*),Fdy(*),Sdy(*),Z(*)
   DOUBLE PRECISION Y,Fdy,Sdy,Z,H,hh,hf,ht,sum1,sum2
!
   sum2 = 0.D0
   IF ( Ndim<1 ) RETURN
   IF ( Ndim/=1 ) THEN
      hh = .5D0*H
      hf = .2D0*H
      ht = .08333333333333333D0*H
!
!     INTEGRATION LOOP
      DO i = 2,Ndim
         sum1 = sum2
         sum2 = sum2 + hh*((Y(i-1)+Y(i))+hf*((Fdy(i-1)-Fdy(i))+ht*(Sdy(i-1)+Sdy(i))))
         Z(i-1) = sum1
      ENDDO
   ENDIF
   Z(Ndim) = sum2
END SUBROUTINE dqhse
