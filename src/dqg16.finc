!*==dqg16.f90 processed by SPAG 8.01RF 00:35  2 Mar 2025

!
!     ..................................................................
!
!        SUBROUTINE DQG16
!
!        PURPOSE
!           TO COMPUTE INTEGRAL(FCT(X), SUMMED OVER X FROM XL TO XU)
!
!        USAGE
!           CALL DQG16 (XL,XU,FCT,Y)
!           PARAMETER FCT REQUIRES AN EXTERNAL STATEMENT
!
!        DESCRIPTION OF PARAMETERS
!           XL     - DOUBLE PRECISION LOWER BOUND OF THE INTERVAL.
!           XU     - DOUBLE PRECISION UPPER BOUND OF THE INTERVAL.
!           FCT    - THE NAME OF AN EXTERNAL DOUBLE PRECISION FUNCTION
!                    SUBPROGRAM USED.
!           Y      - THE RESULTING DOUBLE PRECISION INTEGRAL VALUE.
!
!        REMARKS
!           NONE
!
!        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
!           THE EXTERNAL DOUBLE PRECISION FUNCTION SUBPROGRAM FCT(X)
!           MUST BE FURNISHED BY THE USER.
!
!        METHOD
!           EVALUATION IS DONE BY MEANS OF 16-POINT GAUSS QUADRATURE
!           FORMULA, WHICH INTEGRATES POLYNOMIALS UP TO DEGREE 31
!           EXACTLY. FOR REFERENCE, SEE
!           V.I.KRYLOV, APPROXIMATE CALCULATION OF INTEGRALS,
!           MACMILLAN, NEW YORK/LONDON, 1962, PP.100-111 AND 337-340.
!
!     ..................................................................
!
SUBROUTINE dqg16(Xl,Xu,fct,Y)
   IMPLICIT NONE
!
!
   DOUBLE PRECISION Xl,Xu,Y,a,b,c,fct
!
   a = .5D0*(Xu+Xl)
   b = Xu - Xl
   c = .49470046749582497D0*b
   Y = .13576229705877047D-1*(fct(a+c)+fct(a-c))
   c = .47228751153661629D0*b
   Y = Y + .31126761969323946D-1*(fct(a+c)+fct(a-c))
   c = .43281560119391587D0*b
   Y = Y + .47579255841246392D-1*(fct(a+c)+fct(a-c))
   c = .37770220417750152D0*b
   Y = Y + .62314485627766936D-1*(fct(a+c)+fct(a-c))
   c = .30893812220132187D0*b
   Y = Y + .7479799440828837D-1*(fct(a+c)+fct(a-c))
   c = .22900838882861369D0*b
   Y = Y + .8457825969750127D-1*(fct(a+c)+fct(a-c))
   c = .14080177538962946D0*b
   Y = Y + .9130170752246179D-1*(fct(a+c)+fct(a-c))
   c = .47506254918818720D-1*b
   Y = b*(Y+.9472530522753425D-1*(fct(a+c)+fct(a-c)))
END SUBROUTINE dqg16
